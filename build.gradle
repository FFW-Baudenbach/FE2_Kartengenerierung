plugins {
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id 'idea'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// GitHub-Version >> Docker Version >> Fallback
if (System.getenv("RELEASE_VERSION")) {
	version = System.getenv("RELEASE_VERSION")
}
else if (project.hasProperty("RELEASE_VERSION") && project.getProperty("RELEASE_VERSION") && !project.getProperty("RELEASE_VERSION").allWhitespace) {
	version = project.getProperty("RELEASE_VERSION")
}
else {
	version = "SNAPSHOT"
}
println("Building version $version")

// Omit version number in artifacts for simpler releases
bootJar.archiveFileName = "FE2_Kartengenerierung.jar"

jar {
	enabled = false
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

springBoot {
	buildInfo()
}

// Create zip containing WinSW ServiceWrapper and Jar File
tasks.register('packageDistribution', Zip) {
	archiveFileName = "FE2_Kartengenerierung_WinSW.zip"
	destinationDirectory = file("$layout.buildDirectory/dist")

	from "$layout.buildDirectory/../deploy/winsw", "$layout.buildDirectory/libs"
	dependsOn(assemble)
}
// Adapt docker-compose.yml with correct version
tasks.register('adaptDockerCompose', Copy) {
	from "$layout.buildDirectory/../deploy/docker/docker-compose.yml"
	into "$layout.buildDirectory/dist"
	filter { line -> line.replaceAll('latest', version) }
}

build.dependsOn(adaptDockerCompose)
build.dependsOn(packageDistribution)
